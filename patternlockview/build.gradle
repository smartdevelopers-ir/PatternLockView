
plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
//    alias(libs.plugins.versions) // Access version catalog in buildSrc for plugin
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'patternlockview'

    publishedGroupId = 'com.andrognito.patternlockview'
    libraryName = 'patternlockview'
    artifact = 'patternlockview'

    libraryDescription = 'An easy-to-use, customizable, Material Design complaint Pattern Lock ' +
            'view for Android'

    siteUrl = 'https://github.com/aritraroy/PatternLockView'
    gitUrl = 'https://github.com/aritraroy/PatternLockView.git'

    libraryVersion = '1.1.0'

    developerId = 'aritraroy'
    developerName = 'Aritra Roy'
    developerEmail = 'aritra.roy.in@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]

}

android {
    compileSdkVersion Integer.parseInt(libs.versions.compileSdk.get())
    namespace "com.andrognito.patternlockview"
    defaultConfig {
        minSdkVersion libs.versions.minSdk.get()
        targetSdkVersion libs.versions.maxSdk.get()
        versionCode 100
        versionName "1.1.0"
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}
group = 'com.andrognito.patternlockview'
version = '1.1.0'

dependencies {
    implementation libs.appcompat
    
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                from components.release

                artifactId = artifact

                pom {
                    name = libraryName
                    description = libraryDescription
                    url = siteUrl

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/aritraroy/PatternLockView.git'
                        developerConnection = 'scm:git:ssh://github.com/aritraroy/PatternLockView.git'
                        url = 'http://github.com/aritraroy/PatternLockView'
                    }

                }
            }
        }
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = "${layout.buildDirectory.asFile.get()}/repos/releases"
                def snapshotsRepoUrl = "${layout.buildDirectory.asFile.get()}/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

signing {
    sign configurations.archives
}
